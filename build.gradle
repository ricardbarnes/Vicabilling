import org.gradle.internal.os.OperatingSystem

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.11.4'
}

description = 'VICA billing desktop program'
ext.buildDate = new Date()
version = '1.0'
group = 'com.vonblum'

sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_14

repositories {
    mavenCentral()
}

javafx {
    version = '14.0.2'
    modules = [
            'javafx.controls',
            'javafx.fxml'
    ]
}

application {
    mainClassName = 'com.vonblum.vicabilling.app.Launcher'
    applicationName = 'Vicabilling'
}

dependencies {
    implementation 'org.jetbrains:annotations:19.0.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.8.0'
    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.21'
    compile group: 'com.github.dhorions', name: 'boxable', version: '1.6'
    compile group: 'net.coobird', name: 'thumbnailator', version: '0.4.12'
    testCompile group: 'junit', name: 'junit', version: '4.13'
}

runtime {
    launcher {
        noConsole = true
    }
    jpackage {
        def currentOs = OperatingSystem.current()
        options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
        imageOptions += ["--icon", "src/main/resources/icon/icon.ico"]

        if (currentOs.windows) {
            installerType = 'msi'
            imageName = 'Vicabilling'
            installerName = 'Vicabilling'
            installerOptions += [
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut'
            ]
        } else if (currentOs.linux) {
            installerType = 'deb'
            installerOptions += [
                    '--linux-deb-maintainer', 'ricardbarnes@gmail.com',
                    '--linux-app-category', 'misc'
            ]
        }
    }
}
